
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
    ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Асинх Процедура Подключаемый_СогласованнаяСкидкаПриИзменении(Элемент)
	// ++ Шатица С.А. Задача Домашнее задание 13-7 Дата: 18.10.2024
	Если Объект.Товары.Количество() <> 0 ИЛИ Объект.Услуги.Количество() <> 0 Тогда
		
		Ответ = Ждать ВопросАсинх("Изменен процент скидки. Пересчитать таблицы товаров и услуг?", РежимДиалогаВопрос.ДаНет); 
		
		Если Ответ = КодВозвратаДиалога.Да Тогда
			ПересчитатьТаблицыДокумента();
		КонецЕсли;
		
	КонецЕсли;
	// -- Шатица С.А. Задача Домашнее задание 13-7 Дата: 18.10.2024
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РассчитатьСуммуСтроки(ТекущиеДанные)
	
	// ++ Шатица С.А. Задача Домашнее задание 13-7 Дата: 18.10.2024
	// заменен расчет коэффициента скидки с учетом добавленного реквизита СогласованнаяСкидка
	//КоэффициентСкидки = 1 - ТекущиеДанные.Скидка / 100;
	ОбщаяСкидка = ТекущиеДанные.Скидка + Объект.ДЗ_13_7_СогласованнаяСкидка;
	Если ОбщаяСкидка > 100 Тогда
		ОбщаяСкидка = 100;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("Суммарная скидка по номенклатуре %1 в строке %2 превысила 100%%", ТекущиеДанные.Номенклатура, ТекущиеДанные.НомерСтроки);
		Сообщение.Сообщить();
	КонецЕсли;
	
	КоэффициентСкидки = 1 - ОбщаяСкидка / 100;
	// -- Шатица С.А. Задача Домашнее задание 13-7 Дата: 18.10.2024	
	
	ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество * КоэффициентСкидки;
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДокумента()
	
	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма") + Объект.Услуги.Итог("Сумма");
	
КонецПроцедуры

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Асинх Процедура Подключаемый_КомандаПересчитатьТаблицы(Команда)
	// ++ Шатица С.А. Задача Домашнее задание 13-7 Дата: 18.10.2024
	Ответ = Ждать ВопросАсинх("Пересчитать сумму в таблицах документа с учётом скидки?", РежимДиалогаВопрос.ДаНет); 
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ПересчитатьТаблицыДокумента();
	КонецЕсли;
	
	// -- Шатица С.А. Задача Домашнее задание 13-7 Дата: 18.10.2024
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ПересчитатьТаблицыДокумента()
	// ++ Шатица С.А. Задача Домашнее задание 13-7 Дата: 18.10.2024
	ПересчитатьТаблицу(Объект.Товары);
	
	ПересчитатьТаблицу(Объект.Услуги);
	
	РассчитатьСуммуДокумента();
	// -- Шатица С.А. Задача Домашнее задание 13-7 Дата: 18.10.2024	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьТаблицу(Таблица)
	// ++ Шатица С.А. Задача Домашнее задание 13-7 Дата: 18.10.2024
	Для каждого ТекСтрокаТаблицы Из Таблица Цикл
		РассчитатьСуммуСтроки(ТекСтрокаТаблицы);
	КонецЦикла;
	// -- Шатица С.А. Задача Домашнее задание 13-7 Дата: 18.10.2024
КонецПроцедуры

#КонецОбласти
