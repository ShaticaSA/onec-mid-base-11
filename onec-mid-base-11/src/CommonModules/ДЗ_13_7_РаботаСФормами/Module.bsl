
#Область ПрограммныйИнтерфейс

// Выводит дополнительные реквизиты и команды на форму.
// Для вызова из обработчика формы ПриСозданииНаСервере.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - форма, в которой необходимо добавить элементы.
//
Процедура ДополнитьФорму(Форма) Экспорт

	ИмяФормы = Форма.ИмяФормы;
	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
		ДобавитьПолеСогласованнаяСкидкаИКомандуПересчитатьВГруппаШапкаЛево(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда 
		ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Добавляет элемент КонтактноеЛицо на форму в группу ГруппаШапкаПраво.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - форма, в которой необходимо добавить элементы.
//
Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма)

	ПолеВвода = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ДЗ_13_7_КонтактноеЛицо";

КонецПроцедуры

// Вставляет элемент КонтактноеЛицо на форму перед элементом СуммаДокумента.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - форма, в которой необходимо добавить элементы.
//
Процедура ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма)

	ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), , Форма.Элементы.СуммаДокумента);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ДЗ_13_7_КонтактноеЛицо";

КонецПроцедуры

// Выводит поле СогласованнаяСкидка и команду ПересчитатьТаблицы на форму в группу ГруппаШапкаЛево.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - форма, в которой необходимо добавить элементы.
//
Процедура ДобавитьПолеСогласованнаяСкидкаИКомандуПересчитатьВГруппаШапкаЛево(Форма)
	
	// добавляем новый элемент ГруппаСкидка и помещаем его в ГруппаШапкаЛево
	Группа = Форма.Элементы.Добавить("ГруппаСкидка", Тип("ГруппаФормы"), Форма.Элементы.ГруппаШапкаЛево); 
	Группа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	Группа.ОтображатьЗаголовок = Ложь;
	Группа.Отображение = ОтображениеОбычнойГруппы.Нет;
	Группа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	
	// добавляем поле ввода в ГруппаСкидка и привязываем его к реквизиту СогласованнаяСкидка
	ПолеВвода = Форма.Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"), Группа);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ДЗ_13_7_СогласованнаяСкидка";
	ПолеВвода.УстановитьДействие("ПриИзменении", "Подключаемый_СогласованнаяСкидкаПриИзменении");
	
	// добавляем команду для пересчета таблиц документа с учетом скидки 
	Команда = Форма.Команды.Добавить("ПересчитатьТаблицы");
	Команда.Заголовок = "Пересчитать таблицы";
	Команда.Действие = "Подключаемый_КомандаПересчитатьТаблицы";
	Команда.ИзменяетСохраняемыеДанные = Истина;
	Команда.Картинка = БиблиотекаКартинок.Обновить;
	Команда.Отображение = ОтображениеКнопки.КартинкаИТекст;
	
	// добавляем элемент кнопка для созданной команды и помещаем его в ГруппаСкидка
	КнопкаФормы = Форма.Элементы.Добавить("КнопкаПересчитатьТаблицы", Тип("КнопкаФормы"), Группа);
	КнопкаФормы.ИмяКоманды = "ПересчитатьТаблицы";
	КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	
КонецПроцедуры

#КонецОбласти
